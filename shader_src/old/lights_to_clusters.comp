#version 460 core

// Different sizes yield similar performance results, Intel Iris likely prefers 512
#ifdef INTEGRATED_GPU
    #define LOCAL_SIZE 512
#else
    #define LOCAL_SIZE 32//64
#endif
layout (local_size_x = LOCAL_SIZE, local_size_y = 1, local_size_z = 1) in;

struct PointLight
{
    vec4 position_xyz_range_w;
    vec4 color_rgb_intensity_a;
};


// #define MAX_UNCLIPPED_NGON 10
struct AreaLight
{
    vec4 color_rgb_intensity_a;
    int n;
    int is_double_sided;
    float _packing0, _packing1;
    vec4 aabb_min;
    vec4 aabb_max;
    vec4 sphere_of_influence_center_xyz_radius_w;
    vec4 points_viewspace[MAX_UNCLIPPED_NGON];  // 4th component unused, vec3[] would be packed the same way but vec3 is implemented wrong on some drivers
};

#ifndef CLUSTER_MAX_LIGHTS
    #define CLUSTER_MAX_LIGHTS 100
#endif
struct Cluster
{
    vec4 min_point;
    vec4 max_point;
    uint point_count;
    uint area_count;
    uint point_indices[CLUSTER_MAX_LIGHTS/2];
    uint area_indices[CLUSTER_MAX_LIGHTS/2];
    uint area_light_flags[CLUSTER_MAX_LIGHTS/2];  // 0b00 = neither, 0b01 = diffuse, 0b10 = specular, 0b11 = both
};

layout (std430, binding = 0) restrict buffer point_light_ssbo
{
    PointLight point_lights[];
};

layout (std430, binding = 2) restrict buffer area_light_ssbo
{
    AreaLight area_lights[];
};

layout (std430, binding = 1) restrict buffer cluster_ssbo
{
    Cluster clusters[];
};

// layout (location = 0) uniform mat4 view_matrix;
layout (location = 1) uniform uint num_point_lights;
layout (location = 2) uniform uint num_area_lights;
layout (location = 3) uniform float param_roughness;
layout (location = 4) uniform float param_min_intensity;
layout (location = 5) uniform float param_intensity_saturation;

layout (binding = 7) uniform usamplerCube cluster_normals_cubemap;
layout (binding = 8) uniform sampler1D representative_normals_texture;

#define M_PI 3.1415926535897932384626433832795

struct NormalCone
{
    float half_angle;
    float cos_threshold;
    float sin_threshold;
    vec3 cluster_normal;
};

bool
sphere_aabb_intersection(vec3 center, float radius, vec3 aabb_min, vec3 aabb_max)
{
    // Closest point of AABB to the center of the sphere
    vec3 closest_point = clamp(center, aabb_min, aabb_max);
    closest_point -= center;
    float distance_squared = dot(closest_point, closest_point);
    return distance_squared <= radius * radius;
}

bool
test_sphere_aabb(uint i, Cluster cluster, NormalCone normal_cone)
{
    // Does light affect this cluster based on position
    vec3 light_pos = point_lights[i].position_xyz_range_w.xyz;  // <- lights are already in view space
    float radius = point_lights[i].position_xyz_range_w.w;
    if (!sphere_aabb_intersection(light_pos, radius, cluster.min_point.xyz, cluster.max_point.xyz))
    {
        return false;
    }

#if CLUSTER_NORMALS_COUNT != 1
// // NOTE: Backface culling pointlights not working properly
    // vec3 cluster_center = (cluster.min_point.xyz + cluster.max_point.xyz) * 0.5;
    // vec3 to_light = normalize(light_pos - cluster_center);
    // float cos_omega = dot(normal_cone.cluster_normal, to_light);

    // float cluster_radius = length(cluster.max_point.xyz - cluster.min_point.xyz) * 0.5;
    // float light_dist = length(light_pos - cluster_center);
    // float cos_delta = cluster_radius / light_dist;
    // float delta = acos(clamp(cos_delta, -1.0, 1.0)); // Clamp for numerical stability

    // float threshold = cos(3.14159265 * 0.5 + normal_cone.half_angle + delta);

    // Cull the light if it's completely behind
    // if (cos_omega < threshold)
    //     return false;
#endif

    return true;
}

bool
aabb_aabb_intersect(vec3 aabb1_min, vec3 aabb1_max, 
                         vec3 aabb2_min, vec3 aabb2_max) 
{
    // Bloody epic mate
    // bvec3 overlap = greaterThanEqual(aabb1_max, aabb2_min) &  // & operator not supported on some drivers
    //                lessThanEqual(aabb1_min, aabb2_max);
    // return all(overlap);

    return all(greaterThanEqual(aabb1_max, aabb2_min)) && all(lessThanEqual(aabb1_min, aabb2_max));
}

bool
is_aabb_fully_behind_plane(vec3 aabb_min, vec3 aabb_max, vec3 plane_normal, float plane_constant)
{
    // Plane defined by dot(N,X) + d = 0

    // Get the point in the further direction of the normal
    vec3 furthest_point = vec3(
        plane_normal.x >= 0.0 ? aabb_max.x : aabb_min.x,
        plane_normal.y >= 0.0 ? aabb_max.y : aabb_min.y,
        plane_normal.z >= 0.0 ? aabb_max.z : aabb_min.z
    );

    float signed_distance = dot(plane_normal, furthest_point) + plane_constant;

    // If the signed distance to point closest to the positive side of the plane is negative
    // Then the AABB is behind the plane
    return signed_distance < 0;
}

uint
test_arealight(uint i, Cluster cluster, NormalCone nc)
{
    vec3 cluster_center = (cluster.min_point.xyz + cluster.max_point.xyz) * 0.5;
    vec4 sphere = area_lights[i].sphere_of_influence_center_xyz_radius_w;
    vec3 view_dir = normalize(-cluster_center);

    // Half space rejection for single sided area lights
    if (area_lights[i].is_double_sided == 0)
    {
        vec3 p0 = area_lights[i].points_viewspace[0].xyz;
        vec3 p1 = area_lights[i].points_viewspace[1].xyz;
        vec3 p2 = area_lights[i].points_viewspace[2].xyz;
        vec3 light_normal = cross(p1 - p0, p2 - p0);

        // Fast center test before exact check
        if (dot(cluster_center - p0, light_normal) < 0)
        {
            float plane_constant = -dot(light_normal, p0);
            vec3 furthest_point = mix(cluster.max_point.xyz, cluster.min_point.xyz, lessThan(light_normal, vec3(0)));
            if (dot(light_normal, furthest_point) + plane_constant < 0)
            {
                return 0u;
            }
        }
    }

    // Diffuse test
    bool diffuse_passed = false;
    if (aabb_aabb_intersect(area_lights[i].aabb_min.xyz, area_lights[i].aabb_max.xyz, cluster.min_point.xyz, cluster.max_point.xyz))
    {
        #if CLUSTER_NORMALS_COUNT == 1
            diffuse_passed = sphere_aabb_intersection(sphere.xyz, sphere.w, cluster.min_point.xyz, cluster.max_point.xyz);
        #else
            // Normal based diffuse culling
            vec3 light_dir = normalize(sphere.xyz - cluster_center);
            float cos_theta = dot(nc.cluster_normal, light_dir);
            diffuse_passed = cos_theta > (0.2 - nc.half_angle);
        #endif
    }

    // Specular test
    bool specular_passed = false;
    float distance_to_light = distance(cluster_center, sphere.xyz);
    float effective_distance = max(distance_to_light - sphere.w, 0.0);
    float attenuation = 1.0 / (1.0 + effective_distance * effective_distance);
    float alpha = area_lights[i].color_rgb_intensity_a.a;
    float raw_intensity = alpha * attenuation;
    float scaled_intensity = raw_intensity / (raw_intensity + param_intensity_saturation);
    // float specular_tau = pow(param_min_intensity / (scaled_intensity + 1e-6), 1.0 / param_roughness);
    float specular_tau = exp2(log2(param_min_intensity / (scaled_intensity + 1e-6)) * (1.0/param_roughness));

    #if CLUSTER_NORMALS_COUNT == 1
        // Optimized unified specular test for non-clustered normals
        vec3 light_center = area_lights[i].sphere_of_influence_center_xyz_radius_w.xyz;
        float light_radius = area_lights[i].sphere_of_influence_center_xyz_radius_w.w;
        
        // 1. Conservative light-to-cluster vector
        vec3 light_to_cluster = cluster_center - light_center;
        float dist_to_light = length(light_to_cluster);
        vec3 light_dir = light_to_cluster / (dist_to_light + 1e-6);
        
        // 2. Find most significant light point (dominant specular contributor)
        vec3 dominant_point = light_center + light_radius * reflect(-view_dir, light_dir);
        
        // 3. Half-vector test with roughness adjustment
        vec3 H = normalize(view_dir + normalize(dominant_point - cluster_center));
        float NdotH = dot(view_dir, H); // Using view_dir as proxy for cluster normal
        
        // 4. Roughness-adjusted threshold
        float angular_tolerance = mix(0.05, 0.5, param_roughness); // Wider tolerance for rough mats
        specular_passed = (NdotH > (specular_tau - angular_tolerance)) && 
                        (raw_intensity > param_min_intensity);
    #else
        // float max_dot = 0.0;
        // for (int vertex = 0; vertex < area_lights[i].n; ++vertex)
        // {
        //     vec3 light_vertex_pos = area_lights[i].points_viewspace[vertex].xyz;
        //     vec3 light_to_cluster_dir = normalize(light_vertex_pos - cluster_center);
        //     vec3 H = normalize(view_dir + light_to_cluster_dir);

        //     float H_dot_C = dot(H, nc.cluster_normal);
        //     float term = H_dot_C * nc.cos_threshold + nc.sin_threshold * sqrt(1.0 - H_dot_C * H_dot_C);
        //     float max_N_dot_H = max(1.0, term); // Clamp to 1.0 if inside cone

        //     max_dot = max(max_dot, dot(H, nc.cluster_normal));
        //     max_dot = max(max_dot, max_N_dot_H);
        //     if (max_dot > specular_tau) break;  // Early exit
        // }

        // specular_passed = max_dot > specular_tau;

        // This method doesn't work
        vec3 H = normalize(view_dir + normalize(sphere.xyz - cluster_center));
        float NdotH = dot(H, nc.cluster_normal);
        float cone_adjust = 1.0 / (1.0 + param_roughness * nc.half_angle);
        specular_passed = NdotH > (specular_tau * cone_adjust);
    #endif

    // Seperate diffuse and specular light culling
    uint result = 0u;
    if (diffuse_passed)  result |= 0x1u;
    if (specular_passed) result |= 0x2u;
    return result;
}

void
main()
{
    uint index = gl_GlobalInvocationID.x;
    Cluster cluster = clusters[index];

    // index = tile.x + (tile.y * grid_size.x) + (actual_z * grid_size.x * grid_size.y);
    uint clusters_per_layer = CLUSTER_GRID_SIZE_X * CLUSTER_GRID_SIZE_Y;
    uint combined_z = index / clusters_per_layer;
    uint remainder = index % clusters_per_layer;
    // uint tile_y = remainder / CLUSTER_GRID_SIZE_X;
    // uint tile_x = remainder % CLUSTER_GRID_SIZE_X;
    // uint tile_z = combined_z / CLUSTER_NORMALS_COUNT;
    uint normal_bin = combined_z % CLUSTER_NORMALS_COUNT;

    NormalCone normal_cone;
    float n = sqrt(CLUSTER_NORMALS_COUNT/6);  // since CLUSTER_NORMALS_COUNT= 6n^2
    normal_cone.half_angle = atan(sqrt(2.0) / n);
    // normal_cone.half_angle = atan(1.0 / n);
    normal_cone.cos_threshold = cos(normal_cone.half_angle);
    normal_cone.sin_threshold = sin(normal_cone.half_angle);
    normal_cone.cluster_normal = texture(representative_normals_texture, normal_bin / float(textureSize(representative_normals_texture, 0))).rgb;//representative_normals[normal_bin];

    // // Reinitialize counters to 0 before computing this frames cluster
    // cluster.point_count = 0;
    // cluster.area_count = 0;

    const uint max_point_lights = CLUSTER_MAX_LIGHTS/2;
    const uint max_area_lights = CLUSTER_MAX_LIGHTS/2;

    for (uint i = 0; i < num_point_lights; ++i)
    {
        if (test_sphere_aabb(i, cluster, normal_cone) && cluster.point_count < max_point_lights)
        {
            cluster.point_indices[cluster.point_count++] = i;
        }
    }

    for (uint i = 0; i < num_area_lights; ++i)
    {
        uint contribution_flags = test_arealight(i, cluster, normal_cone);
        if (contribution_flags != 0u && cluster.area_count < max_area_lights)
        {
            cluster.area_indices[cluster.area_count] = i;
            cluster.area_light_flags[cluster.area_count] = contribution_flags;
            ++cluster.area_count;
        }
    }

    clusters[index] = cluster;
}
